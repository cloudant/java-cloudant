/*
 * Copyright Â© 2016, 2017 IBM Corp. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

// Additional configurations for javadoc
configurations {
    linkableJavadoc {
        transitive false
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.6'
    compile project(':cloudant-http')
    linkableJavadoc project(':cloudant-http')
    //test dependencies
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
    testRuntime group: 'org.junit.platform', name: 'junit-platform-console', version: '1.1.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.8.1'
    testCompile group: 'org.jmockit', name: 'jmockit', version: '1.34'
    testCompile group: 'org.littleshoot', name: 'littleproxy', version: '1.1.0'
}

javadoc {
    options.overview = "overview.html"
    include("**/lightcouch/*Exception.java",
            "**/client/api/**")
}

javadocJar {
    // Include the logging.properties example
    from rootProject.rootDir.path + '/logging.properties'
}

gradle.projectsEvaluated {
    javadoc {
        // Add the offline link options for the client API pointing to javadoc.io using the resolved
        // java-cloudant version. Use the package-list from the javadoc zip file.
        configurations.linkableJavadoc.each {
            String moduleName = configurations.linkableJavadoc.resolvedConfiguration.firstLevelModuleDependencies.first().moduleName;
            String moduleVers = configurations.linkableJavadoc.resolvedConfiguration.firstLevelModuleDependencies.first().moduleVersion;
            options.linksOffline("http://static.javadoc.io/com.cloudant/$moduleName/$moduleVers","../$moduleName/build/docs/javadoc");
        }
    }
}

tasks.withType(Test) {
    def jMockit
    doFirst {
        // Do this when the task runs, not before because it will freeze the testCompile config.
        jMockit = project.configurations.testCompile.find {
            it.name.startsWith("jmockit-")
        }
        jvmArgs "-javaagent:${jMockit}"
    }
}

// hacky - see https://discuss.gradle.org/t/passing-arguments-to-a-task/8427/9
def CustomJunitPlatformTask(include, exclude) {
    return tasks.create(name: "exec_${include}_${exclude}", type: JavaExec, dependsOn: testClasses) {
        classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
        main = 'org.junit.platform.console.ConsoleLauncher'
        // arguments to pass to the application
        args ('--reports-dir',testResultsDir,
              '--scan-class-path')
        // build up includes
        if (include.size > 0) {
            args += '--include-tag'
            args += include.join("|")
        }
        // build up excludes
        if (exclude.size > 0) {
            args += '--exclude-tag'
            args += exclude.join("|")
        }
        // inherit system properties
        systemProperties System.properties
        // some tests expect user.dir to be same as working dir, which
        // defaults to project dir
        systemProperty "user.dir", project.projectDir
    }
}

// Run tests which are compatible with all versions of Couch or
// Cloudant.
// This is the default test target.
task test (dependsOn:CustomJunitPlatformTask([],['RequiresCouch','RequiresCloudant']), overwrite: true){}

// Run 'unit' tests which do not require a database.
task noDBTest(dependsOn: CustomJunitPlatformTask([],['RequiresDB'])) {}

// Run tests that can use any Cloudant.
task cloudantTest(dependsOn: CustomJunitPlatformTask([],['RequiresCloudantService'])) {}

// Run all Cloudant service tests.
task cloudantServiceTest(dependsOn: CustomJunitPlatformTask([],['RequiresCloudantLocal', 'RequiresCouch'])) {}

//task for generating a client properties file
class ClientProperties extends DefaultTask {

    //allow this to be configured, default to client.properties
    File clientPropsPath = new File("client.properties")

    //internal
    private Properties p = new Properties()

    def load() {
        //if there is a generated file already load the values
        if (clientPropsPath.exists()) {
            p.load(new FileInputStream(clientPropsPath));
        }
    }


    @TaskAction
    def save() {
        p.put("user.agent.name", project.clientName)
        p.put("user.agent.version", project.version)
        p.store(new FileOutputStream(clientPropsPath), "User agent information for this client")
    }

    String getPropertyValue(String key) {
        return p.getProperty(key)
    }
}

//generate a client props file, make the jar task depend on this
task generateClientPropertiesFile(type: ClientProperties) {
    clientPropsPath = new File(buildDir, "tmp/com.cloudant.client.properties")
    outputs.upToDateWhen {
        if (clientPropsPath.exists()) {
            it.load()
            return project.clientName.equals(it.getPropertyValue("user.agent.name")) && project.version.equals(it.getPropertyValue("user.agent.version"))
        } else {
            return false
        }
    }
}
jar.dependsOn generateClientPropertiesFile
//include the client props in the built jar
jar {
    into "META-INF", { from generateClientPropertiesFile.clientPropsPath }
}

uploadArchives {
    repositories {
        mavenDeployer {

            //augment the pom with additional information
            pom.project {
                description 'Official Cloudant client for Java'
                inceptionYear '2014'
            }
        }
    }
}
